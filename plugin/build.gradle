plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "1.0.0"
}

group = "edu.illinois"
version = "2.1.1"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'edu.illinois:nondex-common:' + version
    implementation 'edu.illinois:nondex-instrumentation:' + version
}

pluginBundle {
    website = 'https://github.com/TestingResearchIllinois/NonDex'
    vcsUrl = 'https://github.com/TestingResearchIllinois/NonDex'
    description = 'NonDex plugin for Gradle'
    tags = ['non-deterministic', 'test']
}

gradlePlugin {
    plugins {
        nondexPlugin {
            id = 'edu.illinois.nondex'
            displayName = 'Nondex plugin for Gradle'
            implementationClass = 'edu.illinois.nondex.gradle.plugin.NondexGradlePlugin'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('test') {
    useJUnitPlatform()
}
